# 5.4 @AspectJ 支持

@AspectJ是一种将aspects声明为用注解来注解Java类的样式。@Aspectj样式是作为Aspectj 5版本的一部分由Aspectj项目引入的。Spring使用和AspectJ相同的注解，它使用了AspectJ提供的用于切入点解析和匹配的库。但是，AOP运行时仍然是纯SpringAOP，并且不依赖于AspectJ编译器或weaver。

## 5.4.1 开启@AspectJ 支持

要在Spring配置中使用@aspectj特性，需要启用Spring支持，以便基于@aspectj特性配置Spring AOP，并根据这些方面是否通知它们来自动代理bean。通过自动代理，我们的意思是，如果Spring确定bean由一个或多个方面通知，它会自动为该bean生成一个代理来拦截方法调用，并确保根据需要执行通知。

可以使用XML或Java风格的配置启用@AspectJ支持。在这两种情况下，您还需要确保aspectj的aaspectjweaver.jar位于应用程序（1.8版或更高版本）的类路径上。这个库可以在AspectJ发行版的lib目录中使用，也可以从Maven中央存储库中使用。

**用Java配置启用@AspectJ支持**

若要使用Java @Configuration 启用@AspectJ支持，请添加@ EnableAspectJAutoProxy注解，如下示例显示：

~~~
@Configuration
@EnableAspectJAutoProxy
public class AppConfig {

}
~~~

**使用XML开启@AspectJ**

要使用基于XML的配置启用@aspectj支持，请使用aop:aspectj autoproxy元素，如下示例所示：

~~~
<aop:aspectj-autoproxy/>
~~~

这假定您使用基于XML模式的配置中描述的模式支持。请参阅AOP模式以了解如何导入AOP命名空间中的标记。

## 5.4.2 定义一个Aspect

在启用@aspectj支持的情况下，应用程序上下文中定义的具有@aspectj方面（具有@aspect注解）类的任何bean都将由spring自动检测并用于配置spring aop。接下来的两个例子展示了一个不太有用的方面所需要的最小定义。

两个示例中的第一个示例显示了应用程序上下文中的常规bean定义，该定义指向具有@aspect注解的bean类：

~~~
<bean id="myAspect" class="org.xyz.NotVeryUsefulAspect">
    <!-- configure properties of the aspect here -->
</bean>
~~~

这两个例子中的第二个展示了notveryusefulAspect类定义，它是用org.aspectj.lang.annotation.Aspect注解的：

~~~
package org.xyz;
import org.aspectj.lang.annotation.Aspect;

@Aspect
public class NotVeryUsefulAspect {

}
~~~

方面（用@aspect注解的类）可以有方法和字段，与任何其他类相同。它们还可以包含切入点、通知和介绍（inter-type）声明。

>通过 component scanning 自动检查AspectJ

>您可以在SpringXML配置中将方面类注册为常规bean，或者通过类路径扫描-自动检测它们，与任何其他Spring Managed bean相同。但是，请注意，@Aspect注解不足以在类路径中自动检测。为此，您需要添加一个单独的@Component注解（或者，根据Spring组件扫描程序的规则，添加一个符合条件的自定义原型注解）。

>使用其他的aspects来通知aspects？

>在SpringAOP中，方面本身不能成为其他方面通知的目标。类上的@Aspect注解将其标记为方面，因此将其从自动代理中排除。

## 5.4.3 定义一个Pointcut

切入点决定连接点，从而使我们能够控制何时执行通知。SpringAOP只支持SpringBean的方法执行连接点，因此您可以将切入点视为匹配SpringBean上方法的执行。一个切入点声明有两部分：一个包含一个名称和任何参数的签名，一个能精确地确定我们感兴趣的执行方法的切入点表达式。在aop的@Aspectj注解样式中，通过常规方法定义提供切入点签名，并使用@Pointcut注解指示切入点表达式（作为切入点签名的方法必须具有void返回类型）。

一个例子可能有助于清晰地区分切入点签名和切入点表达式。下面的示例定义一个名为anyOldTransfer的切入点，该切入点与名为Transfer的任何方法的执行相匹配：

~~~
@Pointcut("execution(* transfer(..))")// the pointcut expression
private void anyOldTransfer() {}// the pointcut signature
~~~

构成@Pointcut注解值的pointcut表达式是一个正则的aspectj 5 pointcut表达式。有关aspectj的切入点语言的完整讨论，请参阅《aspectj编程指南》（和，有关扩展，请参阅aspectj 5开发人员的笔记本）或其中一本关于aspectj的书（例如，由Colyer等人撰写的Eclipse aspectj，或由Ramnivas Laddad撰写的Aspectj in Action）。

**支持的切入点指示符**

SpringAOP支持在切入点表达式中使用的以下AspectJ切入点指示符（PCD）：

* execution：用于匹配方法执行连接点。这是使用SpringAOP时要使用的主要切入点指示符。
* within：限制匹配到特定类型中的连接点（使用SpringAOP时，在匹配类型中声明的方法的执行）。
* this：将匹配限制为连接点（使用SpringAOP时方法的执行），其中bean引用（SpringAOP代理）是给定类型的实例。
* target：限制匹配到连接点（使用SpringAOP时方法的执行），其中目标对象（要代理的应用程序对象）是给定类型的实例。
* args：限制匹配到联接点（使用SpringAOP时方法的执行），其中参数是给定类型的实例。
* @target：限制匹配到连接点（使用SpringAOP时方法的执行），其中执行对象的类具有给定类型的注解。
* @args：限制匹配到联接点（使用SpringAOP时方法的执行），其中传递的实际参数的运行时类型具有给定类型的注解。
* @within：限制与具有给定注解的类型中的联接点匹配（使用SpringAOP时，在具有给定注解的类型中声明的方法的执行）。
* @annotation：限制匹配到连接点的主题（在SpringAOP中执行的方法）具有给定注解的连接点。

>其他的pointcut类型

>完整的AspectJ pointcut语言支持Spring中不支持的其他pointcut指示符：call、get、set、preinitialization、staticinitialization、initialization、handler、adviceexecution、withincode、cflow、cflowbelow、if、@this和@withincode。在SpringAOP解释的切入点表达式中使用这些切入点指示符会导致引发IllegalArgumentException。

>SpringAOP支持的切入点指示符集可以在未来的版本中扩展，以支持更多的AspectJ切入点指示符。

因为SpringAOP只限制与方法执行连接点匹配，所以前面关于切入点指示符的讨论给出了比AspectJ编程指南中更窄的定义。另外，AspectJ本身具有基于类型的语义，并且在执行连接点，this和target都引用同一个对象：执行方法的对象。SpringAOP是一个基于代理的系统，它区分代理对象本身（与this绑定）和代理背后的目标对象（与target绑定）。

>由于Spring的AOP框架基于代理的特性，根据定义，目标对象内的调用不会被拦截。对于JDK代理，只能截获对代理的公共接口方法调用。使用cglib，可以截获代理上的公共和受保护的方法调用（如果需要，甚至可以截获包可见的方法）。但是，通过代理进行的公共交互应该始终通过公共签名进行设计。

>注意，切入点定义通常与任何被截取的方法相匹配。如果一个切入点严格来说是只公开的，即使在有可能通过代理进行非公开交互的CGLIB代理场景中，也需要相应地定义它。

>如果拦截需要在目标类中包含方法调用甚至构造函数，那么考虑使用Spring驱动的native AspectJ weaving，而不是Spring的基于代理的AOP框架。这构成了一种不同的AOP使用模式，具有不同的特性，因此在做出决定之前一定要熟悉weaving。

SpringAOP还支持一个额外的PCD bean。这个PCD允许您将连接点的匹配限制为特定名字的Spring Bean或一组Spring Bean（使用通配符时）。bean PCD具有以下形式：

~~~
bean(idOrNameOfBean)
~~~

idorNameOfBean令牌可以是任何Spring Bean的名称。提供了使用*字符的有限通配符支持，因此，如果为Spring Bean建立了一些命名约定，则可以编写bean PCD表达式来选择它们。与其他切入点指示符一样，bean pcd可以与&amp;&amp;（and）、||（or）和！（否定）运算符一起使用。

>bean PCD仅在SpringAOP中受支持，在native AspectJ编织中不受支持。它是AspectJ定义的标准PCD的特定于Spring的扩展，因此不能用于@Aspect模型中声明的方面。

>bean PCD在实例级别（基于Spring Bean名称概念构建）而不是仅在类型级别（基于编织的AOP受到限制）运行。基于实例的切入点指示符是Spring基于代理的AOP框架的一种特殊功能，它与SpringBean工厂紧密集成，在这里可以自然和简单的按名称标识特定的bean。

**组合切入点表达式**

您可以使用&amp;&amp;、|| 和！组合切入点表达式。也可以按名称引用切入点表达式。下面的示例显示三个切点表达式：

~~~
@Pointcut("execution(public * *(..))")
private void anyPublicOperation() {} 

@Pointcut("within(com.xyz.someapp.trading..*)")
private void inTrading() {} 

@Pointcut("anyPublicOperation() && inTrading()")
private void tradingOperation() {} 
~~~

如前所示，最好使用较小的命名组件构建更复杂的切点表达式。当按名称引用切入点时，使用正常的Java可见性规则（可以在同一类型中看到私有切入点、在层次结构中看到protected切入点、在任何地方看到公共切入点等）。可见性不影响切入点匹配。

**共享公共切入点定义**

在处理企业应用程序时，开发人员通常希望从多个方面引用应用程序的模块和特定的操作集。我们建议定义一个“SystemArchitecture”方面，该方面为此目的捕获公共切入点表达式。此类方面通常类似于以下示例：

~~~
package com.xyz.someapp;

import org.aspectj.lang.annotation.Aspect;
import org.aspectj.lang.annotation.Pointcut;

@Aspect
public class SystemArchitecture {

    /**
     * A join point is in the web layer if the method is defined
     * in a type in the com.xyz.someapp.web package or any sub-package
     * under that.
     */
    @Pointcut("within(com.xyz.someapp.web..*)")
    public void inWebLayer() {}

    /**
     * A join point is in the service layer if the method is defined
     * in a type in the com.xyz.someapp.service package or any sub-package
     * under that.
     */
    @Pointcut("within(com.xyz.someapp.service..*)")
    public void inServiceLayer() {}

    /**
     * A join point is in the data access layer if the method is defined
     * in a type in the com.xyz.someapp.dao package or any sub-package
     * under that.
     */
    @Pointcut("within(com.xyz.someapp.dao..*)")
    public void inDataAccessLayer() {}

    /**
     * A business service is the execution of any method defined on a service
     * interface. This definition assumes that interfaces are placed in the
     * "service" package, and that implementation types are in sub-packages.
     *
     * If you group service interfaces by functional area (for example,
     * in packages com.xyz.someapp.abc.service and com.xyz.someapp.def.service) then
     * the pointcut expression "execution(* com.xyz.someapp..service.*.*(..))"
     * could be used instead.
     *
     * Alternatively, you can write the expression using the 'bean'
     * PCD, like so "bean(*Service)". (This assumes that you have
     * named your Spring service beans in a consistent fashion.)
     */
    @Pointcut("execution(* com.xyz.someapp..service.*.*(..))")
    public void businessService() {}

    /**
     * A data access operation is the execution of any method defined on a
     * dao interface. This definition assumes that interfaces are placed in the
     * "dao" package, and that implementation types are in sub-packages.
     */
    @Pointcut("execution(* com.xyz.someapp.dao.*.*(..))")
    public void dataAccessOperation() {}

}
~~~

您可以在需要切入点表达式的任何地方引用在这样一个方面中定义的切入点。例如，要使服务层具有事务性，可以编写以下内容：

~~~
<aop:config>
    <aop:advisor
        pointcut="com.xyz.someapp.SystemArchitecture.businessService()"
        advice-ref="tx-advice"/>
</aop:config>

<tx:advice id="tx-advice">
    <tx:attributes>
        <tx:method name="*" propagation="REQUIRED"/>
    </tx:attributes>
</tx:advice>
~~~

我们会在基于模式的AOP支持中讨论<aop:config>和<aop:advisor>元素。在事务管理中讨论事务元素。

**例子**

SpringAOP用户最经常使用执行切入点指示符。执行表达式的格式如下：

~~~
execution(modifiers-pattern? ret-type-pattern declaring-type-pattern?name-pattern(param-pattern)
            throws-pattern?)
~~~

除返回类型模式（前面代码段中的ret-type-pattern）、名称模式和参数模式之外的所有部分都是可选的。返回类型模式确定方法的返回类型，以便匹配连接点。\*最常用作返回类型模式。它匹配任何返回类型。只有当方法返回给定类型时，完全限定的类型名才匹配。名称模式与方法名匹配。您可以使用\*通配符作为名称模式的全部或部分。如果指定声明类型模式，请包含后缀.将其连接到名称模式组件。参数模式稍微复杂一点：（）匹配不带参数的方法，而（..）匹配任何数量（零个或多个）的参数。（\*）模式与采用任何类型参数的方法匹配。（*，string）匹配接受两个参数的方法。第一个可以是任何类型，而第二个必须是字符串。有关更多信息，请参阅AspectJ编程指南的语言语义部分。

以下示例显示了一些常见的切入点表达式：

* 执行任何公共方法：
~~~
execution(public * *(..))
~~~

* 执行任何以set开头的方法：
~~~
execution(* set*(..))
~~~

* 执行任何定义在AccountService类的方法：
~~~
execution(* com.xyz.service.AccountService.*(..))
~~~

* 执行任何定义在service包中的方法：
~~~
execution(* com.xyz.service.*.*(..))
~~~

* 执行任何定义在service包或者他的一个子包中的方法：
~~~
execution(* com.xyz.service..*.*(..))
~~~

* 任何在service包中的连接点（仅仅是Spring AOP中执行的方法）
~~~
within(com.xyz.service.*)
~~~

* service包或其子包中的任何连接点（仅在SpringAOP中执行的方法）：
~~~
within(com.xyz.service..*)
~~~

* 任何实现了AccountService接口的代理连接点（仅在SpringAOP中执行的方法）：
~~~
this(com.xyz.service.AccountService)
~~~

>this更加通常被用在绑定form中。

* 任何实现了AccountService接口的目标对象连接点（仅在SpringAOP中执行的方法）：
~~~
target(com.xyz.service.AccountService)
~~~

>target更加通常被用在绑定form中。

* 任何接收一个运行时是Serializable类型参数的连接点（仅在SpringAOP中执行的方法）：
~~~
args(java.io.Serializable)
~~~
>args更加通常被用在绑定form中。

注意，在本例中给出的切入点与execution(* *(java.io.Serializable))不同。如果在运行时传递的参数是Serializable的，则args版本匹配；如果方法签名声明了Serializable的单个参数，则执行版本匹配。

* 任何目标对象有@Transactional 注解的连接点（仅在SpringAOP中执行的方法）：
~~~
@target(org.springframework.transaction.annotation.Transactional)
~~~

>@target也可以被用在绑定form中。

* 目标对象的声明类型具有@transactional注解的任何连接点（仅在Spring AOP中执行的方法）：
~~~
@within(org.springframework.transaction.annotation.Transactional)
~~~

>@within也可以被用在绑定form中。

* 执行方法具有@transactional注解的任何连接点（仅在Spring AOP中执行方法）：
~~~
@annotation(org.springframework.transaction.annotation.Transactional)
~~~

>@annotation也可以被用在绑定form中。

* 接受单个参数的任何连接点（仅在Spring AOP中执行方法），并且传递参数的运行时类型具有@Classified annotation:
~~~
@args(com.xyz.security.Classified)
~~~

>@args也可以被用在绑定form中。

* 名为tradeService的SpringBean上的任何连接点（仅在SpringAOP中执行方法）：
~~~
bean(tradeService)
~~~

* SpringBean上的任何连接点（仅在SpringAOP中执行方法），其名称与通配符表达式*Service匹配：

~~~
bean(*Service)
~~~

**写好切入点**

在编译期间，AspectJ处理切入点以优化匹配性能。检查代码并确定每个连接点是否匹配（静态或动态）给定的切入点是一个代价高昂的过程。（动态匹配意味着无法从静态分析中完全确定匹配，并且在代码中放置一个测试以确定代码运行时是否存在实际匹配）。在第一次遇到切入点声明时，AspectJ将其重写为匹配过程的最佳形式。这是什么意思？基本上，切入点是用DNF（析取标准形式）重写的，切入点的组件被排序，以便首先检查那些评估成本更低的组件。这意味着您不必担心理解各种切入点指示符的性能，并且可以在切入点声明中以任何顺序提供它们。

然而，Aspectj只能处理它被告知的内容。为了获得最佳的匹配性能，您应该考虑他们试图实现什么，并在定义中尽可能地缩小匹配的搜索空间。现有的指示符自然分为三类：kinded, scoping, 和 contextual：
* kinded指示符选择特定类型的连接点：execution, get, set, call, 和 handler.。
* scoping指示符选择一组感兴趣的连接点（可能有多种）：within和withincode
* contextual指示符根据上下文匹配（可选绑定）：this、target和@annotation

写得好的切入点应该至少包括前两种类型（kinded和scoping）。您可以根据连接点上下文包含要匹配的上下文指示符，或者绑定该上下文以在通知中使用。由于额外的处理和分析，可以仅提供一个类似的指示符或上下文指示符，但可能会影响编织性能（使用的时间和内存）。范围指示符匹配非常快，使用它们意味着AspectJ可以很快地消除不应该进一步处理的连接点组。如果可能的话，一个好的切入点应该总是包括一个。
